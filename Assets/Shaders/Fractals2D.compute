#pragma kernel CSMain

#define MAX_DEGREE 10

struct FractalTransform {
    double Size, CenterRe, CenterIm;
};

StructuredBuffer<FractalTransform> TransformBuffer;

RWTexture2D<float4> FractalTex;

bool Julia;
float JuliaRe;
float JuliaIm;
bool Invert;
float4 Coeficients[MAX_DEGREE];

int MaxIter;
int AntialiasingSampling;

int PaletteIndex;

float PseudorandomFloat(float2 seed)
{
    return frac(sin(dot(seed, float2(12.9898, 78.233))) * 43758.5453);
}

//https://www.shadertoy.com/view/ll2GD3
float4 pal(float t, float3 a, float3 b, float3 c, float3 d)
{
    float3 col = a + b * cos(6.28318 * (c * t + d));
    return float4(col.xyz, 1);
}

float4 calculateColor(uint iter)
{
    if (iter == MaxIter)
        return float4(0, 0, 0, 1);
    
    float3 vec1 = float3(1.0, 1.0, 1.0);
    float t = (float) (iter % 50) / 50 + 0.45f;
    
    if (PaletteIndex == 0)
    {
        return pal(t, vec1 / 2, vec1 / 2, vec1, float3(0.0, 0.10, 0.20));
    }

    return pal(t, vec1 / 2, vec1 / 2, vec1, float3(-0.86, 1.02, 1));
}

uint calculateIterations(double2 acum, double2 add)
{
    for (uint i = 0; i < MaxIter; i++)
    {
        double2 lastAcum = acum;
        
        double2 p = double2(1, 0);
        acum = double2(0, 0);
        
        for (int j = 0; j < MAX_DEGREE; j++)
        {
            p = double2(p.x * lastAcum.x - p.y * lastAcum.y, p.x * lastAcum.y + p.y * lastAcum.x);
            acum += p * Coeficients[j].x;
        }
        
        acum += add;

        if (acum.x * acum.x + acum.y * acum.y > 4)
            return i;
    }
    return MaxIter;
}

[numthreads(8, 8, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    uint width;
    uint height;
    FractalTex.GetDimensions(width, height);
    
    FractalTransform transform = TransformBuffer[0];
    
    double re = (double) id.x / height - 0.5 * (double) width / (double) height;
    double im = (double) id.y / height - 0.5;
    double2 coords = double2(re, im) * transform.Size + double2(transform.CenterRe, transform.CenterIm);
    
    if (id.x < width && id.y < height)
    {
        float4 color = float4(0, 0, 0, 0);
        double2 realCoords = coords;
        
        for (int i = 0; i < AntialiasingSampling; i++)
        {
            coords = realCoords;
            double2 offset;
            if (i == 0)
                offset = double2(0, 0);
            else
                offset = double2(
                    PseudorandomFloat(float2(width + i, height + i)) * 2 - 1,
                    PseudorandomFloat(float2(height + 2 * i, width - 2 * i)) * 2 - 1
                );
            
            double pixelSize = transform.Size / (double) height;
            coords += offset * pixelSize / 2;
            
            if (Invert)
            {
                double denominator = coords.x * coords.x + coords.y * coords.y;
                coords = double2(coords.x / denominator, -coords.y / denominator);
            }

            uint iterations = calculateIterations(!Julia ? 0 : coords, !Julia ? coords : double2(JuliaRe, JuliaIm));
            color += calculateColor(iterations);
        }
        
        FractalTex[id.xy] = color / AntialiasingSampling;
    }
}